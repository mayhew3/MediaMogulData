plugins {
    // This plugin allows me to check if there are any updates available for any of the dependencies.
    // From the root directory, run 'gradle dependencyUpdates'. It won't make changes, but print out the latest versions.
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 11
targetCompatibility = 11
group = 'com.mayhew3.mediamogul'
version = '0.6.0'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url 'https://jitpack.io' }
}


dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'info.debatty:java-string-similarity:1.1.0'
    compile 'joda-time:joda-time:2.9.9'
    compile 'org.jsoup:jsoup:1.13.1'
    compile 'org.postgresql:postgresql:42.2.2'
    compile 'org.seleniumhq.selenium:selenium-server:3.141.59'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compile group: 'org.jetbrains', name: 'annotations', version: '17+'
    compile 'com.github.mayhew3:Postgres-Object:0.12.2'
    compile 'com.github.igdb:api-java:2.0.1'
    compile 'com.cloudinary:cloudinary-http44:1.22.1'
    compile 'com.cloudinary:cloudinary-taglib:1.22.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.springframework', name: 'spring-web', version: '5.2.3.RELEASE'

    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'
    compile 'io.socket:socket.io-client:1.0.0'

    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'org.assertj:assertj-joda-time:2.2.0'
    testCompile 'org.mockito:mockito-core:2.18.3'
    testCompile 'com.github.mayhew3:Postgres-Object:0.12.2:tests'
}

jar {
    manifest {
        attributes("Implementation-Title": "Task Schedule Runner",
                "Implementation-Version": version,
                "Main-Class": "com.mayhew3.mediamogul.scheduler.TaskScheduleRunner")
    }
    archiveFileName = "media-mogul-scheduler.jar"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

task runTaskScheduleRunner(type:JavaExec) {
    systemProperty "log4j.configurationFile", "src/main/resources/log4j2-local.xml"
    main = 'com.mayhew3.mediamogul.scheduler.TaskScheduleRunner'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-socketEnv', 'heroku']
}

task runHerokuBackup(type:JavaExec) {
    main = 'com.mayhew3.mediamogul.backup.DataBackupExecutor'
    classpath = sourceSets.main.runtimeClasspath
    args = ['heroku']
}

task runLocalBackup(type:JavaExec) {
    main = 'com.mayhew3.mediamogul.backup.DataBackupExecutor'
    classpath = sourceSets.main.runtimeClasspath
    args = ['local']
}

task runLocalRestoreFromHeroku(type:JavaExec) {
    main = 'com.mayhew3.mediamogul.backup.DataRestoreExecutor'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-env', 'local', '-backupEnv', 'heroku']
}

task runLocalRestoreFromLocal(type:JavaExec) {
    main = 'com.mayhew3.mediamogul.backup.DataRestoreExecutor'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-env', 'local', '-backupEnv', 'local']
}

task runStagingRestoreFromHeroku(type:JavaExec) {
    main = 'com.mayhew3.mediamogul.backup.DataRestoreExecutor'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-env', 'heroku-staging', '-backupEnv', 'heroku', '-appName', 'media-mogul-staging']
}

test {
    exclude 'com/mayhew3/mediamogul/tv/TiVoDaylightSavingsTest.class'
    exclude 'com/mayhew3/mediamogul/tv/TiVoMatchExistingTest.class'
    exclude 'com/mayhew3/mediamogul/tv/TVDBSeriesUpdaterTest.class'
    exclude 'com/mayhew3/mediamogul/model/SchemaTestTest.class'
    exclude 'com/mayhew3/mediamogul/model/SchemaLocalTest.class'
    exclude 'com/mayhew3/mediamogul/model/SchemaEndToEndTest.class'
    exclude 'com/mayhew3/mediamogul/games/SteamUpdaterTest.class'
    exclude 'com/mayhew3/mediamogul/games/IGDBUpdaterTest.class'
}

run {
    args = ['-db', 'Test']
}

mainClassName = 'com.mayhew3.mediamogul.scheduler.TaskScheduleRunner'